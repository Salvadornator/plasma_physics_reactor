SUBROUTINE SPLIPERA(PXIN,PYIN,PYINPP,KNIN,PXOUT,KNOUT,PY,PYP,PYPP,PYINT,PERIOD,KOPT)
  USE prec_rkind
  implicit none
  REAL(RKIND) :: PXIN(KNIN),PYIN(KNIN),PYINPP(KNIN)
  REAL(RKIND) :: XA(KNIN+1),YA(KNIN+1),Y2A(KNIN+1), YINT(KNIN+1)
  REAL(RKIND) :: PXOUT(KNOUT), PY(KNOUT), PYP(KNOUT), PYPP(KNOUT), PYINT(KNOUT), PERIOD
  INTEGER :: KNIN, KNOUT, IIN_XOUT(KNOUT), IPER(KNOUT), KOPT
  !
  REAL(RKIND) :: ZXSHIFT(KNOUT), XAKLO, XAKHI, YAKLO, YAKHI, Y2AKLO, Y2AKHI, H, A, B, A2, B2
  INTEGER :: KLO, KHI, K, J, I, IPREVIOUS
  !
  !   with periodic boundary conditions, SET
  !   XA(N+1) = PXIN(1) + PERIOD
  !   YA(N+1) = PYIN(1)
  !   Y2A(N+1) = PYINPP(1)
  !
  !   Calculates Y, Yprime, Yprimeprime, int(y)
  !
  !-----------------------------------------------------------------------
  ! COMPUTE IIN_XOUT,IPER,ZXSHIFT TO LOCATE PXOUT WITHIN PXIN
  !
  !  CALL FINDINDICESPER(PXIN,KNIN,PXOUT,KNOUT,IIN_XOUT,IPER,ZXSHIFT,PERIOD)
  XA(1:KNIN) = PXIN
  XA(KNIN+1) = PXIN(1) + PERIOD
  YA(1:KNIN) = PYIN
  YA(KNIN+1) = PYIN(1)
  Y2A(1:KNIN) = PYINPP
  Y2A(KNIN+1) = PYINPP(1)
  IF (KOPT .GE. 3) THEN
    ! PREPARE INTEGRAL UP TO XIN(I) TO ADD UP TO LOCAL VALUE
    ! SINCE IT TAKES SOME EXTRA TIME DO IT ONLY IF REQUIRED
    YINT(1) = 0._RKIND
    DO I=2,KNIN+1
      H = XA(I) - XA(I-1)
      YINT(I) = YINT(I-1) +  H/2._RKIND*(YA(I-1) + YA(I) &
        & - H**2/12._RKIND*(Y2A(I-1)+Y2A(I)))
    END DO
  END IF
  !
  IPREVIOUS = 1
  DO J=1,KNOUT
    !     SHIFT PXOUT POINTS WITHIN [PXIN(1),PXIN(KNIN)] WITH IPER * PERIOD
    IF (PXOUT(J) .LT. PXIN(1)) THEN
      IPER(J) = INT((PXIN(1)-PXOUT(J))/PERIOD) + 1
      ZXSHIFT(J) = PXOUT(J) + IPER(J)*PERIOD
    ELSE IF (PXOUT(J) .GT. PXIN(1)+PERIOD) THEN
      IPER(J) = - (INT((PXOUT(J)-PXIN(1)-PERIOD)/PERIOD) + 1)
      ZXSHIFT(J) = PXOUT(J) + IPER(J)*PERIOD
    ELSE
      IPER(J) = 0
      ZXSHIFT(J) = PXOUT(J)
    ENDIF
    ! FIND INTERVAL IN PXIN WHERE PXOUT+-IPER(J)*PERIOD BELONGS, START FROM PREVIOUS INTERVAL, EXCEPT IF THERE WAS A JUMP BACK
    IF (ZXSHIFT(J) .LT. PXIN(IPREVIOUS)) IPREVIOUS = 1
    DO I=IPREVIOUS,KNIN
      IF ((ZXSHIFT(J) .GE. XA(I)) .AND. (ZXSHIFT(J) .LE. XA(I+1))) THEN
        IIN_XOUT(J) = I
        IPREVIOUS = I
        EXIT
      END IF
    END DO
  END DO
  !
  !
  DO J=1,KNOUT
    H = XA(IIN_XOUT(J)+1) - XA(IIN_XOUT(J))
    YAKLO = YA(IIN_XOUT(J))
    YAKHI = YA(IIN_XOUT(J)+1)
    Y2AKLO = Y2A(IIN_XOUT(J))
    Y2AKHI = Y2A(IIN_XOUT(J)+1)
    IF (H.EQ.0._RKIND) STOP 'BAD XA INPUT.'
    A=(XA(IIN_XOUT(J)+1) - ZXSHIFT(J)) / H
    B=1._RKIND - A
    A2=A**2
    B2=B**2
    PY(J)=A*YAKLO+B*YAKHI+ &
      &      (A*(A2-1._RKIND)*Y2AKLO+B*(B2-1._RKIND)*Y2AKHI)*(H**2)/6._RKIND
    IF (KOPT .GE. 1) PYP(J)=(YAKHI-YAKLO)/H - &
      &  ( (3._RKIND*A2-1._RKIND)*Y2AKLO - (3._RKIND*B2-1._RKIND)*Y2AKHI )*H/6._RKIND
    IF (KOPT .GE. 2) PYPP(J)=A*Y2AKLO+B*Y2AKHI
    IF (KOPT .GE. 3) PYINT(J)= YINT(IIN_XOUT(J)) + H/2._RKIND*(YAKLO*(1._RKIND-A2) + YAKHI*B2 &
      &  - (Y2AKLO*(1._RKIND-A2)**2 + Y2AKHI*B2*(2._RKIND-B2))  * H**2/12._RKIND) - IPER(J)*YINT(KNIN+1)
  END DO
  !
  RETURN
END SUBROUTINE SPLIPERA
