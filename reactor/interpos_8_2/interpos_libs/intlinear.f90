!.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-.-
!
SUBROUTINE INTLINEAR(PXIN,PYIN,KNIN,PXOUT,PYOUT,PYOUTP,PYOUTPP,PYOUTINT,KNOUT,KOPTDER,KEXTRAPO)
  !
  !   COMPUTE LINEAR INTERPOLATION OF (PXIN,PYIN) ON (PXOUT,PYOUT).
  !
  !   KOPTDER = 0: COMPUTE ONLY PYOUT (PYOUTP, PYOUTPP NOT USED)
  !   KOPTDER = 1: COMPUTE ALSO 1ST DER. IN PYOUTP (PYOUTPP NOT USED)
  !   KOPTDER = 2: AS 1 AND 2ND DER. IN PYOUTPP
  !   KOPTDER = 3: AS 2 AND INTEGRAL FROM (PXIN(1) IN PYOUTPP
  !
  !   KEXTRAPO = 0: PRINT MESSAGE AND RETURN IF NEED TO EXTRAPOLATE
  !   KEXTRAPO =  1: LINEAR EXTRAPOLATION WITH LAST POINT AND DERIVATIVE
  !   KEXTRAPO = -1: LINEAR EXTRAPOLATION WITH LAST 2 POINTS (same as +1, thus use +1)
  !   KEXTRAPO =  10: Y=Y_EDGE FOR EXTRAPOLATION => CONSTANT EXTRAPOLATION
  !   KEXTRAPO = -10: Y=0 FOR EXTRAPOLATION
  !
  USE PREC_RKIND
  IMPLICIT NONE
  ! arguments
  INTEGER :: KNIN, KNOUT, KOPTDER, KEXTRAPO
  REAL(RKIND) :: PXIN(KNIN), PYIN(KNIN), PXOUT(KNOUT)
  REAL(RKIND) :: PYOUT(KNOUT), PYOUTP(KNOUT), PYOUTPP(KNOUT), PYOUTINT(KNOUT)
  !
  REAL(RKIND) :: ZYINT_XIN(KNIN)
  ! FOR FUNCTIONS
  REAL(RKIND) :: X1, X2, F1, F2, PX, FLINEAR, FLINEARP, FLINEARM1, &
       & ZYP, P1, FLINXYP, FLINXYPM1
  INTEGER :: K, KLO, KHI, I, J
  !
  FLINEAR(X1,F1,X2,F2,PX) = F1 + (PX-X1)/(X2-X1) * (F2-F1)
  FLINEARP(X1,F1,X2,F2) = (F2-F1) / (X2-X1)
  FLINEARM1(X1,F1,X2,F2,PX) = (PX-X1)*(F1+0.5_RKIND*(PX-X1)*(F2-F1)/(X2-X1))
!!$  FLINXYP(X1,F1,P1,PX) = P1*(PX-X1) + F1
!!$  FLINXYPM1(X1,F1,P1,PX) = (PX-X1)*(F1+0.5_RKIND*P1*(PX-X1))
  !.......................................................................
  !
  IF (KOPTDER .GE. 3) THEN
    ZYINT_XIN(1) = 0._RKIND
    DO I=2,KNIN
      ZYINT_XIN(I) = ZYINT_XIN(I-1) + 0.5_RKIND*(PXIN(I)-PXIN(I-1))*(PYIN(I)+PYIN(I-1))
    END DO
  END IF
  !
  DO J=1,KNOUT
    IF (PXOUT(J) .LT. PXIN(1)) THEN
      SELECT CASE (KEXTRAPO)
      CASE (-1, 1)
        PYOUT(J) = FLINEAR(PXIN(1),PYIN(1),PXIN(2),PYIN(2),PXOUT(J))
        IF (KOPTDER .GE. 1) PYOUTP(J) = FLINEARP(PXIN(1),PYIN(1),PXIN(2),PYIN(2))
        IF (KOPTDER .GE. 2) PYOUTPP(J) = 0._RKIND
        IF (KOPTDER .GE. 3) PYOUTINT(J) = FLINEARM1(PXIN(1),PYIN(1),PXIN(2),PYIN(2),PXOUT(J))
      CASE (0)
        PRINT *,' WARNING POINT PXOUT(',J,')=',PXOUT(J),' OUTSIDE INTERVAL ON LEFT'
        RETURN
      CASE (10)
        PYOUT(J) = PYIN(1)
        IF (KOPTDER .GE. 1) PYOUTP(J) = 0._RKIND
        IF (KOPTDER .GE. 2) PYOUTPP(J) = 0._RKIND
        IF (KOPTDER .GE. 3) PYOUTINT(J) = PYIN(1)*(PXOUT(J)-PXIN(1))
      CASE (-10)
        PYOUT(J) = 0._RKIND
        IF (KOPTDER .GE. 1) PYOUTP(J) = 0._RKIND
        IF (KOPTDER .GE. 2) PYOUTPP(J) = 0._RKIND
        IF (KOPTDER .GE. 3) PYOUTINT(J) = 0._RKIND
      CASE DEFAULT
        WRITE(0,*) 'DEFAULT IN INTLINEAR PXOUT(J) .LT. PXIN(1) SELECT CASE'
        WRITE(0,*) 'SHOULD NOT BE THERE, OPTION KEXTRAPO = ',KEXTRAPO,' NOT DEFINED'
        CALL FLUSH(0)
        RETURN
      END SELECT
      !
    ELSE IF (PXOUT(J) .GT. PXIN(KNIN)) THEN
      ! POINT ON RIGHT OF INTERVAL
      SELECT CASE (KEXTRAPO)
      CASE (-1, 1)
        PYOUT(J) = FLINEAR(PXIN(KNIN-1),PYIN(KNIN-1),PXIN(KNIN),PYIN(KNIN),PXOUT(J))
        IF (KOPTDER .GE. 1) PYOUTP(J) = FLINEARP(PXIN(KNIN-1),PYIN(KNIN-1),PXIN(KNIN),PYIN(KNIN))
        IF (KOPTDER .GE. 2) PYOUTPP(J) = 0._RKIND
        IF (KOPTDER .GE. 3) PYOUTINT(J) = ZYINT_XIN(KNIN) + &
          & FLINEARM1(PXIN(KNIN),PYIN(KNIN),PXIN(KNIN-1),PYIN(KNIN-1),PXOUT(J))
      CASE (0)
        PRINT *,' WARNING POINT PXOUT(',J,')=',PXOUT(J),' OUTSIDE INTERVAL ON RIGHT'
        RETURN
      CASE(10)
        PYOUT(J) = PYIN(KNIN)
        IF (KOPTDER .GE. 1) PYOUTP(J) = 0.0_RKIND
        IF (KOPTDER .GE. 2) PYOUTPP(J) = 0.0_RKIND
        IF (KOPTDER .GE. 3) PYOUTINT(J) = ZYINT_XIN(KNIN) + PYIN(KNIN)*(PXOUT(J)-PXIN(KNIN))
      CASE(-10)
        PYOUT(J) = 0.0_RKIND
        IF (KOPTDER .GE. 1) PYOUTP(J) = 0.0_RKIND
        IF (KOPTDER .GE. 2) PYOUTPP(J) = 0.0_RKIND
        IF (KOPTDER .GE. 3) PYOUTINT(J) = ZYINT_XIN(KNIN)
      CASE DEFAULT
        WRITE(0,*) 'DEFAULT IN INTLINEAR PXOUT(J) .GT. PXIN(1) SELECT CASE'
        WRITE(0,*) 'SHOULD NOT BE THERE, OPTION KEXTRAPO = ',KEXTRAPO,' NOT DEFINED'
        CALL FLUSH(0)
        RETURN
      END SELECT
      !
    ELSE
      ! FIND PXIN INTERVAL BY BI-SECTION
      KLO=1
      KHI=KNIN
10    CONTINUE
      IF (KHI-KLO.GT.1) THEN
        K=(KHI+KLO)/2
        IF(PXIN(K) .GT. PXOUT(J))THEN
          KHI=K
        ELSE
          KLO=K
        ENDIF
        GOTO 10
      ENDIF
      PYOUT(J) = FLINEAR(PXIN(KLO),PYIN(KLO),PXIN(KHI),PYIN(KHI),PXOUT(J))
      IF (KOPTDER .GE. 1) PYOUTP(J) = FLINEARP(PXIN(KLO),PYIN(KLO),PXIN(KHI),PYIN(KHI))
      IF (KOPTDER .GE. 2) PYOUTPP(J) = 0._RKIND
      IF (KOPTDER .GE. 3) PYOUTINT(J) = ZYINT_XIN(KLO) + &
        & FLINEARM1(PXIN(KLO),PYIN(KLO),PXIN(KHI),PYIN(KHI),PXOUT(J))
      !
    END IF
  END DO
  !
  RETURN
END SUBROUTINE INTLINEAR
